# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>

_pkgname=fizz
pkgname=${_pkgname}-weekly
pkgver=2022.09.19.00
pkgrel=1
pkgdesc="C++14 implementation of the TLS-1.3 standard"
arch=("x86_64")
url="https://github.com/facebookincubator/fizz"
license=("BSD")
conflicts=("fizz")
provides=("fizz")
depends=("folly-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('f3e0753a17f56d294006e20ef9c4af89336a0844cff325b407b1ae6cb98baea90b1b84b76ae2f7596cf553fdd85e199a78800a45215c75c06fe14c9cd82f6576')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DBUILD_TESTS=ON \
          -DBUILD_EXAMPLES=OFF
    cmake --build build
}

check() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake --build build --target test
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
