# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>

_pkgname=fizz
pkgname=${_pkgname}-weekly
pkgver=2022.10.17.00
pkgrel=1
pkgdesc="C++14 implementation of the TLS-1.3 standard"
arch=("x86_64")
url="https://github.com/facebookincubator/fizz"
license=("BSD")
conflicts=("fizz")
provides=("fizz")
depends=("folly-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('327ff5af0e7ef73ad4e5187416ba3650f1cd73e0aec711db3da148d527fa7d7b0d89f445718a81675f638a552e7c73c7639ca1086faa661ad09ed9427cec5e49')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DBUILD_TESTS=ON \
          -DBUILD_EXAMPLES=OFF
    cmake --build build
}

check() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake --build build --target test
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
