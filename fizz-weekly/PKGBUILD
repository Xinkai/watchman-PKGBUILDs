# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>

_pkgname=fizz
pkgname=${_pkgname}-weekly
pkgver=2022.06.27.00
pkgrel=1
pkgdesc="C++14 implementation of the TLS-1.3 standard"
arch=("x86_64")
url="https://github.com/facebookincubator/fizz"
license=("BSD")
conflicts=("fizz")
provides=("fizz")
depends=("folly-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=("b7ff96991aa63d0b2c9d16c291068e936aa7b702b3e18433c0be2e5a23169a4b088738dde993e5d0dac63b06ff995c37532dcd00a7043e67064bf07dede0b1c0")
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DBUILD_TESTS=ON \
          -DBUILD_EXAMPLES=OFF
    cmake --build build
}

check() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake --build build --target test
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
