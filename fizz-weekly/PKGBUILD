# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>

_pkgname=fizz
pkgname=${_pkgname}-weekly
pkgver=2023.01.23.00
pkgrel=1
pkgdesc="C++14 implementation of the TLS-1.3 standard"
arch=("x86_64")
url="https://github.com/facebookincubator/fizz"
license=("BSD")
conflicts=("fizz")
provides=("fizz")
depends=("folly-weekly")
makedepends=("boost=1.81.0" "cmake" "gtest" "gmock")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('5e4e851f459d1c882a5602ebd75fe233f0d26045c11aafc82fd42c3d2bf6ed8868216a063ecc230f884f8e812823e70cde67c4157362c46a3955b4494456d1a1')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DBUILD_TESTS=OFF \
          -DBUILD_EXAMPLES=OFF
    cmake --build build
}

check() {
    cd "$_pkgname-$pkgver/$_pkgname"
    # cmake --build build --target test
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
