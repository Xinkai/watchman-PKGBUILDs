# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>

_pkgname=fizz
pkgname=${_pkgname}-weekly
pkgver=2022.11.07.00
pkgrel=1
pkgdesc="C++14 implementation of the TLS-1.3 standard"
arch=("x86_64")
url="https://github.com/facebookincubator/fizz"
license=("BSD")
conflicts=("fizz")
provides=("fizz")
depends=("folly-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('385fc0de97a314fd3aef58bcda86ce999961ca477642d8bbcfe212d14d48bd8bb9d1972fbfdcacdfa6be4de9a137f48a14d0b730cc7346c9a767195591b0f66b')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DBUILD_TESTS=OFF \
          -DBUILD_EXAMPLES=OFF
    cmake --build build
}

check() {
    cd "$_pkgname-$pkgver/$_pkgname"
    # cmake --build build --target test
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
