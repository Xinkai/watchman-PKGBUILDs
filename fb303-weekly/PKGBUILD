# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fb303
pkgname=${_pkgname}-weekly
pkgver=2022.10.17.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fb303"
license=("Apache")
depends=("folly-weekly" "fizz-weekly" "wangle-weekly" "fbthrift-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('b97d4025aac7a8e18715d3858afadb7f962a7686314ed925fa20413be109c97043e9b29ecf8fee6b51a6807f293e51b8e58a8a56341c6e37d91d5573acae5c9e')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"

    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPYTHON_EXTENSIONS=OFF \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
