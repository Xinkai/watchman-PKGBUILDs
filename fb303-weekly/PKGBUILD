# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fb303
pkgname=${_pkgname}-weekly
pkgver=2023.12.11.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fb303"
license=("Apache")
depends=("folly-weekly" "fizz-weekly" "wangle-weekly" "fbthrift-weekly")
makedepends=("boost=1.83.0" "cmake" "gtest" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('403d9579004d93ca642eb4539617193619638ac4a9b7f168308168a9fbe4ce5f1a85cb0de389b4b2b1a004748ba8124a4f0029f1490787e43af76c4b3f8298f0')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"

    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPYTHON_EXTENSIONS=OFF \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
