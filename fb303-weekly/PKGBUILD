# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fb303
pkgname=${_pkgname}-weekly
pkgver=2023.01.30.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fb303"
license=("Apache")
depends=("folly-weekly" "fizz-weekly" "wangle-weekly" "fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gtest" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('7c4adfa77add68832d83790b1a700fc2a95492d39b749512c6f0e521d02cdac8141f164f75d79e3798cefafd4dde424c89699a9bb67efead2c8aaea93cc2ef87')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"

    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPYTHON_EXTENSIONS=OFF \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
