# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fb303
pkgname=${_pkgname}-weekly
pkgver=2023.03.27.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fb303"
license=("Apache")
depends=("folly-weekly" "fizz-weekly" "wangle-weekly" "fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gtest" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('90c2517dbf477bcc2b0b022f4df6d3d8c52d5a3b5bef3387701a01909d914d38b4071ed79c378090294a7309141ca09bfc8b5eca9ba9ba434e03f04f12ee1ed7')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"

    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPYTHON_EXTENSIONS=OFF \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
