# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fb303
pkgname=${_pkgname}-weekly
pkgver=2022.10.03.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fb303"
license=("Apache")
depends=("folly-weekly" "fizz-weekly" "wangle-weekly" "fbthrift-weekly")
makedepends=("cmake" "gtest" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('b92e91b21248383599746cc104666df0102974bc763461d3fc399d95f48633b6c4c85f17971772b8870852c737d95e2cb4fad1d1521ad3365253f92851e606fa')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"

    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DPYTHON_EXTENSIONS=OFF \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
