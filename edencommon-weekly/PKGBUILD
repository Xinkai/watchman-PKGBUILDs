# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=edencommon
pkgname=${_pkgname}-weekly
pkgver=2023.05.08.00
pkgrel=1
pkgdesc="A shared library for Meta's source control filesystem tools (EdenFS and Watchman)"
arch=("x86_64")
url="https://github.com/facebookexperimental/edencommon"
license=("Apache")
depends=("fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("edencommon")
provides=("edencommon")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('ce0a0b3105a9faae8a3657f300914619084e7106c68b08ed879f714220ae2d5b4b1b88f97bb2bf5c490ba1eaf8b15e47dd4b6fe8592992cf79ee7f2cd98c02b8')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
