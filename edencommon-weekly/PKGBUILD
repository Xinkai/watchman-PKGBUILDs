# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=edencommon
pkgname=${_pkgname}-weekly
pkgver=2024.02.12.00
pkgrel=1
pkgdesc="A shared library for Meta's source control filesystem tools (EdenFS and Watchman)"
arch=("x86_64")
url="https://github.com/facebookexperimental/edencommon"
license=("Apache")
depends=("folly-weekly")
makedepends=("boost=1.83.0" "cmake" "gmock" "folly-weekly")
conflicts=("edencommon")
provides=("edencommon")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('c834846b4c3b6d096ffea5db725486abde84943eb46e66ccdfc7462f1e5f57a0c9158b99d1188ba24d87bb3819d35073b07c123ade7ced0334332e3a55dac625')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
