# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=edencommon
pkgname=${_pkgname}-weekly
pkgver=2023.01.30.00
pkgrel=1
pkgdesc="A shared library for Meta's source control filesystem tools (EdenFS and Watchman)"
arch=("x86_64")
url="https://github.com/facebookexperimental/edencommon"
license=("Apache")
depends=("fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("edencommon")
provides=("edencommon")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('1e2ed7a9d41cf151a75f4f3fa577093b7894932f3ad84fa23cec3e1d86f627f4bcaa69bd590c7d04b9e7620af021955d6ce58b2ae4b47b87cbd56ad0ce0140a7')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
