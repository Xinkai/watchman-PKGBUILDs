# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=edencommon
pkgname=${_pkgname}-weekly
pkgver=2023.03.13.00
pkgrel=1
pkgdesc="A shared library for Meta's source control filesystem tools (EdenFS and Watchman)"
arch=("x86_64")
url="https://github.com/facebookexperimental/edencommon"
license=("Apache")
depends=("fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("edencommon")
provides=("edencommon")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('ac15fac0dd66d8c78d905973cef6cb9d9db418f07f7715286bc6a946d2175efb4b3917d83f6820c0ff0f5ca1c0d95b0324165c511db2ba9389cd77ec4a7c15e7')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
