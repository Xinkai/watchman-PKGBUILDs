# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=edencommon
pkgname=${_pkgname}-weekly
pkgver=2023.03.27.00
pkgrel=1
pkgdesc="A shared library for Meta's source control filesystem tools (EdenFS and Watchman)"
arch=("x86_64")
url="https://github.com/facebookexperimental/edencommon"
license=("Apache")
depends=("fbthrift-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("edencommon")
provides=("edencommon")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('0f0bbcedd295fba8215104e5223782211d0e8eab3f1215f313dab094223eb665a6e8c32d8c4e19adf7f8abed6925ed528c62456c1a75c9bc2cdaa421621aa9ca')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
