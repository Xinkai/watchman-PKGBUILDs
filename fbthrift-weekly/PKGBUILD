# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fbthrift
pkgname=${_pkgname}-weekly
pkgver=2022.10.03.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fbthrift"
license=("Apache")
depends=(
  "folly-weekly"
  "fizz-weekly"
  "wangle-weekly"
)
makedepends=("cmake" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('0e264ba3d28b28f9f69871852a39c79dd082c55315bb501506a9a03c77dcb209e76c1396cfde3b565a249db7fc947b5527316b91edf4a37c8b0d511b31b5b4e0')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/"
    cmake -S . -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
