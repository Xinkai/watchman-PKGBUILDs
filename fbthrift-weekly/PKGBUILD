# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fbthrift
pkgname=${_pkgname}-weekly
pkgver=2022.09.19.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fbthrift"
license=("Apache")
depends=(
  "folly-weekly"
  "fizz-weekly"
  "wangle-weekly"
)
makedepends=("cmake" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('bd2134d4500cdbb21bfac36458f6789478f3523e73e7aec24f71211f9ca6b35db889d836d47c49b6bb04987d3962e945360142e07c74e684d1f57ffb5e275fc1')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/"
    cmake -S . -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
