# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fbthrift
pkgname=${_pkgname}-weekly
pkgver=2023.01.30.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fbthrift"
license=("Apache")
depends=(
  "folly-weekly"
  "fizz-weekly"
  "wangle-weekly"
)
makedepends=("boost=1.81.0" "cmake" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('f917e8d50ae795a4157b677b9855d1f2e21828010ddc8d2d2db9abc9612b3d8de5a65259fc36d89d0155016ee2e91f5480697b2a8f7f9b8e1103354540076bda')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/"
    cmake -S . -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
