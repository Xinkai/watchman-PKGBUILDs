# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fbthrift
pkgname=${_pkgname}-weekly
pkgver=2023.01.23.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fbthrift"
license=("Apache")
depends=(
  "folly-weekly"
  "fizz-weekly"
  "wangle-weekly"
)
makedepends=("boost=1.81.0" "cmake" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('91af4a7ea6dfcc8e5d2c70bb2b0c356873f260faeed7a9a5f8c375a5046d4248aedc14fe83357781ef6afb0dab4041c1d43949f66b25494328552cc224256824')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/"
    cmake -S . -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
