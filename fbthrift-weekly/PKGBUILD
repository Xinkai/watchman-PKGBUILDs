# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=fbthrift
pkgname=${_pkgname}-weekly
pkgver=2022.10.31.00
pkgrel=1
pkgdesc="Facebook's branch of Apache Thrift, including a new C++ server."
arch=("x86_64")
url="https://github.com/facebook/fbthrift"
license=("Apache")
depends=(
  "folly-weekly"
  "fizz-weekly"
  "wangle-weekly"
)
makedepends=("cmake" "gmock")
source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('867ef29946c3380d269911d2133297e3b326752d2071f981bdeddf0948f26f518c274ba33cead2de29b0832ec09373356af25575629c7e1f78ab94fc25ec120f')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/"
    cmake -S . -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build build --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
