# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=mvfst
pkgname=${_pkgname}-weekly
pkgver=2023.08.14.00
pkgrel=1
pkgdesc="An implementation of the QUIC transport protocol."
arch=("x86_64")
url="https://github.com/facebook/mvfst"
license=("Apache")
depends=("folly-weekly" "fizz-weekly")
makedepends=("boost=1.81.0" "cmake" "clang")
conflicts=("mvfst")
provides=("mvfst")
source=("$url/archive/v${pkgver}.tar.gz")
b2sums=('5329d1d875af96a33f77dc49b745cae12c839da4e082319d9855b64a14e48c8fbd2eafbdb21c6fb0db69a9a7105a8556f1d9654c6725bb1ee438f4a8665a4350')
options=(!lto)

build() {
  cd "$_pkgname-$pkgver"
  cmake \
    -DCMAKE_C_COMPILER=/usr/bin/clang \
    -DCMAKE_CXX_COMPILER=/usr/bin/clang++ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64_v3 \
    -DBUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  # cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
