# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: Xiaoxu Guo <ftiasch0@gmail.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

_pkgname=folly
pkgname=${_pkgname}-weekly
pkgver=2022.05.23.00
pkgrel=1
pkgdesc="An open-source C++ library developed and used at Facebook"
arch=("x86_64")
url="https://github.com/facebook/folly"
license=("Apache")
depends=("snappy" "jemalloc" "double-conversion" "gflags" "google-glog" "libevent" "boost>=1.51.0"
         "boost-libs" "fmt>=8.1.1-3" "openssl" "zlib" "bzip2" "xz" "lz4" "zstd" "libsodium"
         "libdwarf" "libaio" "liburing" "libunwind")
makedepends=("cmake" "gtest" "gmock" "clang")
conflicts=("folly")
provides=("folly")
source=("$url/archive/v${pkgver}.tar.gz")
b2sums=("15fb1ade0fd6626dad5dfa45bebe437af58d898058fd71a8546a6ca361321069f5e1be41a057dc7e71a7b47a517eae19d1e18c64040b548923c2ea139842346c")
options=(!lto)

build() {
  cd "$_pkgname-$pkgver"
  ASMFLAGS="${CFLAGS}" cmake \
    -DCMAKE_C_COMPILER=clang \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64 \
    -DBUILD_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
