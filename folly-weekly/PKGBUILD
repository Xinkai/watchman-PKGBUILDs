# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: Xiaoxu Guo <ftiasch0@gmail.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

_pkgname=folly
pkgname=${_pkgname}-weekly
pkgver=2022.10.17.00
pkgrel=1
pkgdesc="An open-source C++ library developed and used at Facebook"
arch=("x86_64")
url="https://github.com/facebook/folly"
license=("Apache")
depends=("snappy" "jemalloc" "double-conversion" "gflags" "google-glog" "libevent" "boost>=1.79.0"
         "boost-libs" "fmt" "openssl" "zlib" "bzip2" "xz" "lz4" "zstd" "libsodium"
         "libdwarf" "libaio" "liburing" "libunwind")
makedepends=("cmake" "gtest" "gmock" "clang")
conflicts=("folly")
provides=("folly")
source=("$url/archive/v${pkgver}.tar.gz")
b2sums=('86f23c31f499e9dbc95a9fa58c722f1379d21aea934eb45dbe8cc5e4cfb907b7e30b8704717f4609ee90307558c4ed89e7c42a9a6c214161dbdc9745ec709886')
options=(!lto)

build() {
  cd "$_pkgname-$pkgver"
  cmake \
    -DCMAKE_C_COMPILER=/usr/bin/clang \
    -DCMAKE_CXX_COMPILER=/usr/bin/clang++ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64_v3 \
    -DBUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  # cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
