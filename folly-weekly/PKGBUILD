# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: Xiaoxu Guo <ftiasch0@gmail.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

_pkgname=folly
pkgname=${_pkgname}-weekly
pkgver=2024.02.26.00
pkgrel=2
pkgdesc="An open-source C++ library developed and used at Facebook"
arch=("x86_64")
url="https://github.com/facebook/folly"
license=("Apache")
depends=("snappy" "jemalloc" "double-conversion" "gflags" "google-glog=0.7.1" "libevent"
         "boost-libs=1.83.0" "fmt" "openssl>=3.0" "zlib" "bzip2" "xz" "lz4" "zstd" "libsodium"
         "libdwarf" "libaio" "liburing" "libunwind")
makedepends=("boost=1.83.0" "cmake" "gtest" "gmock")
conflicts=("folly")
provides=("folly")
source=(
  "$url/archive/v${pkgver}.tar.gz"
  "fix-glog-0.7.patch"
  "fix-elf-include-unistd-h.patch"
)
b2sums=('56e7f1c4a877c02d483e8e52a6d36afe8094f4bfa1fc4ee9c8f427fc51427cec978c3c29c0156aa1625f0db35800fc40903e96ac603d276edf9612bafbc327a6'
        '961c17f69d724f3970ea719f88fc4efb0e77304fa5dee5697c0bea42ede1e2fa58bda8ad7c3c344ecb7a0c2d3893674edfe3a5b446880c4b049f1752eb63f0d5'
        'a743e3b0ea56027b9342112f9be2222500f6f53af201cacbc4f62f696edc44ec4e6aa9360ad3471c5c071c0a050ea8f5bbc9c4aeff0047387a21a767e40956a1')
options=(!lto)

prepare() {
  cd "$_pkgname-$pkgver"
  patch CMakeLists.txt < ../fix-glog-0.7.patch
  patch folly/experimental/symbolizer/Elf.cpp < ../fix-elf-include-unistd-h.patch
}

build() {
  cd "$_pkgname-$pkgver"
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64_v3 \
    -DBUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  # cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
