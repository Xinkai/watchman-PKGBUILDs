# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: Xiaoxu Guo <ftiasch0@gmail.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

_pkgname=folly
pkgname=${_pkgname}-weekly
pkgver=2023.05.08.00
pkgrel=1
pkgdesc="An open-source C++ library developed and used at Facebook"
arch=("x86_64")
url="https://github.com/facebook/folly"
license=("Apache")
depends=("snappy" "jemalloc" "double-conversion" "gflags" "google-glog" "libevent"
         "boost-libs=1.81.0" "fmt" "openssl>=3.0" "zlib" "bzip2" "xz" "lz4" "zstd" "libsodium"
         "libdwarf" "libaio" "liburing" "libunwind")
makedepends=("boost=1.81.0" "cmake" "gtest" "gmock" "clang")
conflicts=("folly")
provides=("folly")
source=("$url/archive/v${pkgver}.tar.gz")
b2sums=('c6f8ce73df07a3b64ebbd944b2bef86ba2e5fbee37804c9a2e930fb2071c6892946d68c57951c93e13b2d13c2a5590043438940e2e5f2883461b7b5cdf7ae5fd')
options=(!lto)

build() {
  cd "$_pkgname-$pkgver"
  cmake \
    -DCMAKE_C_COMPILER=/usr/bin/clang \
    -DCMAKE_CXX_COMPILER=/usr/bin/clang++ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64_v3 \
    -DBUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  # cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
