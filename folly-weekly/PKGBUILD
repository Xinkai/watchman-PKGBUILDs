# Maintainer: Xinkai Chen <xinkai.chen@qq.com>
# Maintainer: Xiaoxu Guo <ftiasch0@gmail.com>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

_pkgname=folly
pkgname=${_pkgname}-weekly
pkgver=2024.02.12.00
pkgrel=1
pkgdesc="An open-source C++ library developed and used at Facebook"
arch=("x86_64")
url="https://github.com/facebook/folly"
license=("Apache")
depends=("snappy" "jemalloc" "double-conversion" "gflags" "google-glog" "libevent"
         "boost-libs=1.83.0" "fmt" "openssl>=3.0" "zlib" "bzip2" "xz" "lz4" "zstd" "libsodium"
         "libdwarf" "libaio" "liburing" "libunwind")
makedepends=("boost=1.83.0" "cmake" "gtest" "gmock")
conflicts=("folly")
provides=("folly")
source=("$url/archive/v${pkgver}.tar.gz")
b2sums=('ff4b7ff3934b17543188ee554dad10a8c28d05ebd8b06d33fac9b7a644587ac0437f8315e3fa03b194914f0f9224ba395a9f60da5fab0196cc8cac177734a1c0')
options=(!lto)

build() {
  cd "$_pkgname-$pkgver"
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_LIBRARY_ARCHITECTURE=x86_64_v3 \
    -DBUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -S . -B _build
  cmake --build _build
}

check() {
  cd "$_pkgname-$pkgver"
  # cmake --build _build --target test
}

package() {
  cd "$_pkgname-$pkgver"
  cmake --build _build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
