# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=wangle
pkgname=${_pkgname}-weekly
pkgver=2022.10.31.00
pkgrel=1
pkgdesc="C++ networking library providing client/server abstractions for building services"
arch=("x86_64")
url="https://github.com/facebook/wangle"
license=("Apache")
depends=("fizz-weekly" "folly-weekly")
makedepends=("cmake" "gmock")
conflicts=("wangle")
provides=("wangle")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('b42bb36f6f49cb00851c35ad75ff62e0397c465332961eb2727f2b8181f771fb45ca6b461adbca27d2063ce436f60a3b9cf940742d9e92fe120fbb0503d479d4')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
