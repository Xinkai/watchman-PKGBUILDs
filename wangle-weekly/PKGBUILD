# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=wangle
pkgname=${_pkgname}-weekly
pkgver=2023.05.01.00
pkgrel=1
pkgdesc="C++ networking library providing client/server abstractions for building services"
arch=("x86_64")
url="https://github.com/facebook/wangle"
license=("Apache")
depends=("fizz-weekly" "folly-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("wangle")
provides=("wangle")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('5bf01d4e81d09882b9370980d31335fddbfe3937c201532108f8f44f0fb73d75295ff82a0d237c99e3ccf289f2d3cb3bfa9e55823852afcf0f9ce9da854d9d81')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
