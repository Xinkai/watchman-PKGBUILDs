# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=wangle
pkgname=${_pkgname}-weekly
pkgver=2023.05.08.00
pkgrel=1
pkgdesc="C++ networking library providing client/server abstractions for building services"
arch=("x86_64")
url="https://github.com/facebook/wangle"
license=("Apache")
depends=("fizz-weekly" "folly-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("wangle")
provides=("wangle")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('784d1ecf9d8e6eace2d2b8bc274e4d2ceaf8b41b082eb644a359caf986bdfe980ad01103c4cbb1c486e1d579479c454fc82d724b760777b936ae8c2755022656')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
