# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=wangle
pkgname=${_pkgname}-weekly
pkgver=2023.03.13.00
pkgrel=1
pkgdesc="C++ networking library providing client/server abstractions for building services"
arch=("x86_64")
url="https://github.com/facebook/wangle"
license=("Apache")
depends=("fizz-weekly" "folly-weekly")
makedepends=("boost=1.81.0" "cmake" "gmock")
conflicts=("wangle")
provides=("wangle")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('8dd1664806ed41c974ffe0e8148186cfef8f47faead35b84453504118b3a5e83bb613bd1c1d69cd64c13cb16183f4330ed98a4870cdcb16402e8392cd18a6b72')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
