# Maintainer: Xinkai Chen <xinkai.chen@qq.com>

_pkgname=wangle
pkgname=${_pkgname}-weekly
pkgver=2022.09.26.00
pkgrel=1
pkgdesc="C++ networking library providing client/server abstractions for building services"
arch=("x86_64")
url="https://github.com/facebook/wangle"
license=("Apache")
depends=("fizz-weekly" "folly-weekly")
makedepends=("cmake" "gmock")
conflicts=("wangle")
provides=("wangle")
source=("${url}/archive/v${pkgver}.tar.gz")
b2sums=('c5dcdeea85ee151fc812154dfa427c6576c4d2367b8637f8bdf2f981ebc616e4e20041f3f517d4a006682f4c04f1e15baab9aae3196877c5dd5b1b01bf04de2d')
options=(!lto)

build() {
    cd "$_pkgname-$pkgver/$_pkgname"
    cmake -S . -B build \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd "$_pkgname-$pkgver"
    cmake --build "$_pkgname/build" --target install -- DESTDIR="$pkgdir/"

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
